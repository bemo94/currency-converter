apply plugin: 'kotlin-multiplatform'
apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.native.cocoapods'
apply plugin: "com.squareup.sqldelight"

version = "1.0"

sqldelight {
    SqlDelightDatabase {
        packageName = "com.octo.project"
        sourceFolders = ["sqldelight"]
        schemaOutputDirectory = file("build/dbs")
    }
}

android {
    compileSdkVersion 29
    testOptions.unitTests.includeAndroidResources = true
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        //This is for MultiplatformSettings
        debug {
            // MPP libraries don't currently get this resolution automatically
            matchingFallbacks = ['release']
        }

    }
}

kotlin {
    targets {
        final def iosTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") ? presets.iosArm64 : presets.iosX64

        fromPreset(iosTarget, 'ios') {
            binaries {
                framework('SharedCode') {
                    embedBitcode "bitcode"
                }
            }

            // add this to get sqldelight works on ios
            compilations.each {
                it.extraOpts("-linker-options", "-lsqlite3")
            }
        }



        fromPreset(presets.android, 'android')
    }

    sourceSets {
        commonMain.dependencies {
            api 'org.jetbrains.kotlin:kotlin-stdlib-common'
        }

        commonTest.dependencies {
            implementation 'org.jetbrains.kotlin:kotlin-test-common'
        }

        androidMain.dependencies {
            api 'org.jetbrains.kotlin:kotlin-stdlib'
            implementation "com.squareup.sqldelight:android-driver:1.2.0"
        }

        iosMain.dependencies {
            implementation "com.squareup.sqldelight:ios-driver:1.2.0"
        }

    }

    cocoapods {
        // Configure fields required by CocoaPods.
        summary = "Some description for a Kotlin/Native module"
        homepage = "Link to a Kotlin/Native module homepage"
    }
}

